START -> SEMANTICEPSILON REPTSTART0 SEMANTICCLASSDECLORFUNCDEF  .  
SEMANTICCLASSDECLORFUNCDEF -> .

APARAMS -> EXPR SEMANTICEPSILON REPTAPARAMS1 SEMANTICAPARAMS   . 
APARAMS ->  .  
SEMANTICAPARAMS ->  .

APARAMSTAIL -> comma EXPR   . 

ADDOP -> plus   . 
ADDOP -> minus   . 
ADDOP -> or   . 

ARITHEXPR -> TERM SEMANTICEPSILON RIGHTRECARITHEXPR SEMANTICARITHEXPR  . 
SEMANTICARITHEXPR ->  .

ARRAYSIZE -> lsqbr ARRAYSIZE2  . 
ARRAYSIZE2 -> intlit SEMANTICINTLIT rsqbr  . 
ARRAYSIZE2 -> rsqbr  . 
SEMANTICINTLIT ->   .

ASSIGNOP -> equal   . 

CLASSDECL -> SEMANTICEPSILON class id SEMANTICTOKEN OPTCLASSDECL2 lcurbr REPTCLASSDECL4 SEMANTICCLASSDECL rcurbr semi   . 
SEMANTICCLASSDECL ->  .

CLASSDECLORFUNCDEF -> CLASSDECL   . 
CLASSDECLORFUNCDEF -> FUNCDEF   . 

EXPR -> SEMANTICEPSILON ARITHEXPR EXPR2 SEMANTICEXPR . 

EXPR2 -> RELOP SEMANTICTOKEN ARITHEXPR   . 
EXPR2 ->  . 
SEMANTICEXPR ->  .

FPARAMS -> SEMANTICEPSILON id SEMANTICTOKEN colon TYPE SEMANTICTOKEN SEMANTICEPSILON REPTFPARAMS3 SEMANTICARRAYSIZE REPTFPARAMS4 SEMANTICFPARAMS   . 
FPARAMS ->  . 
SEMANTICFPARAMS ->  .

FPARAMSTAIL -> comma id SEMANTICTOKEN colon TYPE SEMANTICTOKEN SEMANTICEPSILON REPTFPARAMS3 SEMANTICARRAYSIZE  . 

FACTOR -> SEMANTICEPSILON FUNCTIONCALLORVARIABLE SEMANTICFACTORCALLORVAR SEMANTICFACTOR   . 
FACTOR -> intlit SEMANTICTOKEN SEMANTICFACTOR   . 
FACTOR -> floatlit SEMANTICTOKEN SEMANTICFACTOR   . 
FACTOR -> lpar ARITHEXPR rpar SEMANTICFACTOR   . 
FACTOR -> not SEMANTICTOKEN FACTOR SEMANTICFACTOR  . 
FACTOR -> SIGN SEMANTICTOKEN FACTOR SEMANTICFACTOR  . 
SEMANTICTOKEN ->  .
SEMANTICFACTOR ->  .
SEMANTICFACTORCALLORVAR ->  .

FUNCBODY -> lcurbr SEMANTICEPSILON REPTFUNCBODY1 SEMANTICFUNCBODY rcurbr   . 
SEMANTICFUNCBODY ->   .

FUNCDEF -> SEMANTICEPSILON FUNCHEAD FUNCBODY  SEMANTICFUNCDEF  . 
SEMANTICFUNCDEF ->  .

FUNCHEAD -> function id SEMANTICTOKEN FUNCHEAD3  . 

FUNCHEAD2 -> id SEMANTICTOKEN lpar FPARAMS rpar arrow RETURNTYPE SEMANTICTOKEN SEMANTICFUNCARROW   . 
FUNCHEAD2 -> constructor lpar FPARAMS rpar SEMANTICFUNCCONSTSTRUCT  . 
SEMANTICFUNCARROW ->  .
SEMANTICFUNCCONSTSTRUCT ->  .

FUNCHEAD3 -> SEMANTICEPSILON sr FUNCHEAD2  . 
FUNCHEAD3 -> SEMANTICEPSILON lpar FPARAMS rpar arrow RETURNTYPE SEMANTICTOKEN SEMANTICFUNCARROW   . 

VARIABLE -> id SEMANTICTOKEN SEMANTICEPSILON VARIABLE3 SEMANTICVARIABLE  . 
VARIABLE3 -> INDICE . 
VARIABLE3 -> VARIABLE2  . 
VARIABLE3 ->  . 
VARIABLE2 -> dot SEMANTICTOKEN id SEMANTICEPSILON VARIABLE4 SEMANTICVARIABLE  . 
VARIABLE4 -> lpar APARAMS rpar VARIABLE2  . 
VARIABLE4 -> INDICE VARIABLE5  . 
VARIABLE5 -> VARIABLE2  . 
VARIABLE5 ->  . 

FUNCTIONCALLORVARIABLE -> id SEMANTICTOKEN FUNCTIONCALLORVARIABLE1  . 
FUNCTIONCALLORVARIABLE1 -> SEMANTICEPSILON SEMANTICEPSILON INDICELOOP SEMANTICINDICELIST SEMANTICVARIABLE FUNCTIONCALLORVARIABLE2  . 
FUNCTIONCALLORVARIABLE1 -> SEMANTICEPSILON lpar APARAMS rpar SEMANTICFUNCTIONCALL FUNCTIONCALLORVARIABLE2  . 
FUNCTIONCALLORVARIABLE2 -> dot id SEMANTICTOKEN FUNCTIONCALLORVARIABLE3  . 
FUNCTIONCALLORVARIABLE2 ->  . 
FUNCTIONCALLORVARIABLE3 -> SEMANTICEPSILON SEMANTICEPSILON INDICELOOP SEMANTICINDICELIST SEMANTICVARIABLE FUNCTIONCALLORVARIABLE2  . 
FUNCTIONCALLORVARIABLE3 -> SEMANTICEPSILON lpar APARAMS rpar SEMANTICFUNCTIONCALL FUNCTIONCALLORVARIABLE2  . 
SEMANTICVARIABLE ->   .
SEMANTICFUNCTIONCALL ->   .

INDICE -> lsqbr ARITHEXPR rsqbr   . 

LOCALVARDECL -> localvar id SEMANTICID colon TYPE SEMANTICTYPE LOCALVARDECL2  . 
LOCALVARDECL2 -> SEMANTICEPSILON REPTFPARAMS3 SEMANTICARRAYSIZE SEMANTICVARDECL semi   . 
LOCALVARDECL2 -> lpar APARAMS rpar SEMANTICVARDECL semi   .
SEMANTICID ->   .
SEMANTICTYPE ->   .
SEMANTICEPSILON ->   . 
SEMANTICARRAYSIZE ->   .
SEMANTICVARDECL ->   .

LOCALVARDECLORSTMT -> LOCALVARDECL   . 
LOCALVARDECLORSTMT -> STATEMENT   . 

MEMBERDECL -> SEMANTICEPSILON MEMBERFUNCDECL   . 
MEMBERDECL -> SEMANTICEPSILON MEMBERVARDECL   . 

MEMBERFUNCDECL -> function id SEMANTICTOKEN colon lpar FPARAMS rpar arrow RETURNTYPE SEMANTICTOKEN SEMANTICMEMBERFUNCDECL semi   . 
MEMBERFUNCDECL -> constructor colon lpar FPARAMS rpar SEMANTICMEMBERFUNCDECL semi   . 
SEMANTICMEMBERFUNCDECL ->  .

MEMBERVARDECL -> attribute id SEMANTICTOKEN colon TYPE SEMANTICTOKEN SEMANTICEPSILON REPTFPARAMS3 SEMANTICARRAYSIZE SEMANTICMEMBERVARDECL semi   . 
SEMANTICMEMBERVARDECL ->  .

MULTOP -> mult   . 
MULTOP -> div   . 
MULTOP -> and   . 

OPTCLASSDECL2 -> isa SEMANTICEPSILON id SEMANTICTOKEN REPTOPTCLASSDECL22 SEMANTICISA   . 
OPTCLASSDECL2 ->  .  
SEMANTICISA ->  .

RELEXPR -> ARITHEXPR RELOP SEMANTICTOKEN ARITHEXPR SEMANTICRELEXPR   . 
SEMANTICRELEXPR ->   .

RELOP -> eq   . 
RELOP -> neq   . 
RELOP -> lt   . 
RELOP -> gt   . 
RELOP -> leq   . 
RELOP -> geq   . 

REPTSTART0 -> CLASSDECLORFUNCDEF REPTSTART0   . 
REPTSTART0 ->  .  

REPTAPARAMS1 -> APARAMSTAIL REPTAPARAMS1   . 
REPTAPARAMS1 ->  .  

REPTCLASSDECL4 -> VISIBILITY SEMANTICTOKEN MEMBERDECL REPTCLASSDECL4   . 
REPTCLASSDECL4 ->  .  

REPTFPARAMS3 -> ARRAYSIZE REPTFPARAMS3   . 
REPTFPARAMS3 ->  .  

REPTFPARAMS4 -> FPARAMSTAIL REPTFPARAMS4   . 
REPTFPARAMS4 ->  .  

REPTFUNCBODY1 -> LOCALVARDECLORSTMT REPTFUNCBODY1   . 
REPTFUNCBODY1 ->  . 

REPTOPTCLASSDECL22 -> comma id SEMANTICTOKEN REPTOPTCLASSDECL22   . 
REPTOPTCLASSDECL22 ->  .  

REPTSTATBLOCK1 -> STATEMENT REPTSTATBLOCK1   . 
REPTSTATBLOCK1 ->  .  

RETURNTYPE -> TYPE   . 
RETURNTYPE -> void   . 

RIGHTRECARITHEXPR ->  .  
RIGHTRECARITHEXPR -> ADDOP SEMANTICTOKEN TERM RIGHTRECARITHEXPR   . 

RIGHTRECTERM ->  .  
RIGHTRECTERM -> MULTOP SEMANTICTOKEN FACTOR RIGHTRECTERM   . 

SIGN -> plus   . 
SIGN -> minus   . 

STATBLOCK -> lcurbr SEMANTICEPSILON REPTSTATBLOCK1 rcurbr SEMANTICSTATBLOCK   . 
STATBLOCK -> STATEMENT   . 
STATBLOCK ->  .  
SEMANTICSTATBLOCK ->  .

STATEMENT -> FUNCTIONCALLORASIGNSTAT semi   . 
STATEMENT -> SEMANTICEPSILON if lpar RELEXPR rpar then STATBLOCK else STATBLOCK SEMANTICIFSTAT semi   . 
STATEMENT -> SEMANTICEPSILON while lpar RELEXPR rpar STATBLOCK SEMANTICWHILESTAT semi   . 
STATEMENT -> read lpar VARIABLE rpar SEMANTICREADSTAT semi   . 
STATEMENT -> write lpar EXPR rpar SEMANTICWRITESTAT semi   . 
STATEMENT -> return lpar EXPR rpar SEMANTICRETURNSTAT semi  . 
SEMANTICRETURNSTAT ->  .
SEMANTICWRITESTAT ->  .
SEMANTICREADSTAT ->  .
SEMANTICIFSTAT ->  .
SEMANTICWHILESTAT ->  .

FUNCTIONCALLORASIGNSTAT -> SEMANTICEPSILON id SEMANTICTOKEN ISFUNCTIONCALLORVARIABLE  .

ISFUNCTIONCALLORVARIABLE -> lpar APARAMS rpar AFTERFUNCTIONCALL  .
ISFUNCTIONCALLORVARIABLE -> SEMANTICEPSILON INDICELOOP SEMANTICINDICELIST AFTERVARIABLE  .

AFTERFUNCTIONCALL -> dot id SEMANTICTOKEN MIDDLESTATE  .
AFTERVARIABLE -> dot id SEMANTICTOKEN MIDDLESTATE  .

MIDDLESTATE -> SEMANTICEPSILON INDICELOOP SEMANTICINDICELIST AFTERVARIABLE  . 
MIDDLESTATE -> lpar APARAMS rpar AFTERFUNCTIONCALL  . 

AFTERVARIABLE -> ENDASSIGN  . 
AFTERFUNCTIONCALL ->  SEMANTICFUNCTIONCALLSTAT. 

INDICELOOP -> INDICE INDICELOOP  . 
INDICELOOP ->  . 
 
ENDASSIGN -> ASSIGNOP SEMANTICTOKEN EXPR SEMANTICASSIGNSTAT. 
SEMANTICINDICELIST ->  .
SEMANTICASSIGNSTAT ->  .
SEMANTICFUNCTIONCALLSTAT ->  .

TERM -> SEMANTICEPSILON FACTOR RIGHTRECTERM SEMANTICTERM   . 
SEMANTICTERM ->   .

TYPE -> integer   . 
TYPE -> float   . 
TYPE -> id   . 

VISIBILITY -> public   . 
VISIBILITY -> private   . 
VISIBILITY ->  .  
